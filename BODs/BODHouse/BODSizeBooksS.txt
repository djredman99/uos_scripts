// This macro is working with stacks of boxes filled with BOD books
// The macro will ensure that no book has more than 305 BODs in it
// This is useful as this is the max number of BODs you can have in a book and still have 2 BOD books in your pack (124 item limit, room to remove one BOD at a time)
// It will prep all of your books so that you can put any 2 BOD books in your pack and combine them to make a full 500 BOD book for selling
clearignorelist
if not @findobject 'TargetBookS'
  promptalias 'TargetBookS'
endif
if list 'SmithBoxes' == 0
  removelist 'SmithBoxes'
  msg 'BOXES RESET'
endif
// Boxes in House
if not listexists 'SmithBoxes'
  @createlist 'SmithBoxes'
  // Stack 1
  @pushlist 'SmithBoxes' 0x40949b5b
  @pushlist 'SmithBoxes' 0x40949a0d
  @pushlist 'SmithBoxes' 0x4094f26b
  @pushlist 'SmithBoxes' 0x4094ed89
  // Stack 2
  @pushlist 'SmithBoxes' 0x4094a6e4
  @pushlist 'SmithBoxes' 0x40949f38
  @pushlist 'SmithBoxes' 0x4097c845
  @pushlist 'SmithBoxes' 0x40949902
  // Stack 3
  @pushlist 'SmithBoxes' 0x4094daa7
  @pushlist 'SmithBoxes' 0x4094a528
  @pushlist 'SmithBoxes' 0x4094a028
  @pushlist 'SmithBoxes' 0x4094d9ce
  // Stack 4
  @pushlist 'SmithBoxes' 0x40949df1
  @pushlist 'SmithBoxes' 0x4094d7ec
  @pushlist 'SmithBoxes' 0x4094d915
  @pushlist 'SmithBoxes' 0x4094d710
  // Stack 5
  //@pushlist 'SmithBoxes' 0x42a0a893
  //@pushlist 'SmithBoxes' 0x42a0a9d6
  //@pushlist 'SmithBoxes' 0x42a02d65
  //@pushlist 'SmithBoxes' 0x42a02e32
endif
while list 'SmithBoxes' > 0
  closegump 'container' 'currentbookSB'
  unsetalias 'currentboxSB'
  unsetalias 'currentbookSB'
  if injournal 'The world will save' 'system'
    clearjournal
    sysmsg 'Pausing macro for Server Save'
    waitforjournal 'World save complete' 20000 'system'
    sysmsg 'Resuming macro'
  endif
  if not @findobject SmithBoxes[0]
    sysmsg 'Cannot find BOX!!!' 0xf00
    stop
  endif
  setalias 'currentboxSB' 'found'
  while distance 'currentboxSB' > 1
    turn 'north'
    walk 'north'
    pause 600
  endwhile
  sysmsg 'Box found' 0xf00
  useobject 'currentboxSB'
  pause 600
  while @findtype 0x2259 0x0 'currentboxSB' 1 5
    sysmsg 'Found a Book...' 0xf00
    pause 600
    if @property 'Deeds In Book' 'found' > 305
      moveitem 'found' 'backpack'
      pause 1000
      setalias 'currentbookSB' 'found'
      sysmsg 'Book Found and moved' 0xf00
      pause 2000
      useobject 'currentbookSB'
      waitforgump 0x54f555df 15000
      while @property 'Deeds In Book' 'found' > 305
        replygump 0x54f555df 5
        pause 600
      endwhile
      while @findtype 0x2258 1102 'backpack'
        moveitem 'found' 'TargetBookS' 65535 65535 0
        waitforgump 0x54f555df 2000
        pause 600
      endwhile
      moveitem 'currentbookSB' 'currentboxSB'
      ignoreobject 'currentbookSB'
      pause 600
    else
      ignoreobject 'found'
      pause 600
    endif
  endwhile
  poplist 'SmithBoxes' 'front'
endwhile
msg "DONE"
